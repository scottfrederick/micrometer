buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        constraints {
            classpath('org.ow2.asm:asm:7.3.1') {
                because 'Supports modern JDKs'
            }
        }
    }

    configurations.classpath.resolutionStrategy.cacheDynamicVersionsFor 0, 'minutes'
}

apply plugin: 'io.micrometer.release'
apply from: 'dependencies.gradle'

allprojects {
    group = 'io.micrometer'

    afterEvaluate { project -> println "I'm configuring $project.name with version $project.version" }
}

subprojects {
    description = 'Application monitoring instrumentation facade'

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot/" } // for context-propagation snapshots
    }
}

configure(libraryProjects) {
    apply plugin: 'java-library'
    apply plugin: 'io.micrometer.publish'
    apply plugin: 'io.micrometer.compare'
    apply plugin: 'io.micrometer.conventions'

    // All projects use optional annotations, but since we don't expose them downstream we would
    // have to add the dependency in every project, which is tedious so just do it here.
    dependencies {
        // JSR-305 only used for non-required meta-annotations
        optionalApi "com.google.code.findbugs:jsr305:latest.release"
    }

}

configure(sampleProjects) {
    apply plugin: 'java'
    apply plugin: 'io.micrometer.conventions'
}

def getBomProject() {
    [project(":micrometer-bom")] as Set
}

def getSampleProjects() {
    subprojects.findAll {project -> project.name.contains("samples") || project.name.contains('benchmarks')}
}

def getLibraryProjects() {
    subprojects - sampleProjects - bomProject
}

wrapper {
    gradleVersion = '7.5.1'
}

defaultTasks 'build'
